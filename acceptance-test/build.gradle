plugins {
    id 'groovy'
    id 'java-gradle-plugin'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

dependencies {
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

gradlePlugin {
    pluginSourceSet parent.sourceSets.main
}

fileTree("$projectDir/examples") {
    include '**/build.gradle'
}.each { file ->
    final exampleProjectDir = file.path - '/build.gradle'
    final exampleProjectRelativeDir = relativePath(exampleProjectDir - 'examples/')
    final exampleProjectPath = exampleProjectRelativeDir.replace('/', ':')
    final escaped = exampleProjectRelativeDir.replace('/', '-')
    task("build-$escaped", type: JavaExec, group: 'acceptance-test') {
        classpath = sourceSets.test.runtimeClasspath
        main = 'org.hidetake.gradle.swagger.generator.test.Main'
        args = ["$exampleProjectPath:build"]
    }
    clean.dependsOn task("clean-$escaped", type: Delete, group: 'clean') {
        delete "$exampleProjectDir/build"
    }
}

task('examples', description: 'Generate example documents', group: 'acceptance-test') {
    dependsOn 'build-ui-v3-basic'
    doLast {
        copy {
            from "$projectDir/examples/ui-v3/basic/build/swagger-ui-petstore"
            into "$buildDir/examples/ui-v3"
        }
    }
    dependsOn 'build-redoc-basic'
    doLast {
        copy {
            from "$projectDir/examples/redoc/basic/build/redoc-petstore"
            into "$buildDir/examples/redoc"
        }
    }
    dependsOn 'build-codegen-v3-html'
    doLast {
        copy {
            from "$projectDir/examples/codegen-v3/html/build/swagger-code-petstore"
            into "$buildDir/examples/html"
        }
    }
}
